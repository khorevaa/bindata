#Использовать tempfiles

Перем КлассыПолученияДанных;
Перем КешПутейКлассов;

Функция ПолучитьФайл(Знач ИмяФайла, Знач КорневаяПапка, Знач ИмяКлассаПолучения) Экспорт

	Если ЕстьКлассПолучения(ИмяКлассаПолучения) Тогда
		Возврат ПолучитьПутьИзКласса(ИмяФайла, ИмяКлассаПолучения);
	КонецЕсли;

	Возврат ОбъединитьПути(КорневаяПапка, ИмяФайла);
	
КонецФункции

Функция ПолучитьПутьИзКласса(Знач ИмяФайла, ИмяКлассаПолучения) Экспорт

	КлючКеша = СтрШаблон("%1_%2", ИмяКлассаПолучения, ИмяФайла);

	ПутьИхКеша = КешПутейКлассов[КлючКеша];

	Если НЕ ПутьИхКеша = Неопределено Тогда
		Возврат ПутьИхКеша;
	КонецЕсли;

	НовыйВременныйФайл = ВременныеФайлы.НовоеИмяФайла();

	ЗаписатьДанныеВФайл(НовыйВременныйФайл, ПолучитьДанныеФайла(ИмяФайла, ИмяКлассаПолучения));

	КешПутейКлассов.Вставить(КлючКеша, НовыйВременныйФайл);

	Возврат НовыйВременныйФайл;
	
КонецФункции

Функция ПолучитьДанныеФайла(ИмяФайла, ИмяКлассаПолучения)
	
	Возврат КлассыПолученияДанных[ИмяКлассаПолучения].ПолучитьДанные(ИмяФайла);

КонецФункции

Процедура ЗаписатьДанныеВФайл(ИмяВременногоФайла, XMLДанныеФайла)
	
	ДвоичныеДанные = Base64Значение(XMLДанныеФайла);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);

КонецПроцедуры

Функция ЕстьКлассПолучения(ИмяКлассаПолучения)

	ЗначениеКеша = КлассыПолученияДанных[ИмяКлассаПолучения];
	КлассДоступен = НЕ ЗначениеКеша = Неопределено;
	Если ЗначениеКеша = Неопределено Тогда
		
		КлассДоступен = Ложь;
		Попытка
			КлассПолучения = Новый ("bindata"+ИмяКлассаПолучения);
			КлассДоступен = Истина;
		Исключение

		КонецПопытки;

		Если КлассДоступен Тогда
			КлассыПолученияДанных.Вставить(ИмяКлассаПолучения, КлассПолучения);
		КонецЕсли;

	КонецЕсли;

	Возврат КлассДоступен;

КонецФункции

Процедура ОчиститьКаталогОтКлассовBindata(Знач РабочийКаталог)

	НайденныеФайлы = НайтиФайлы(РабочийКаталог, "bindata*.os", Истина );

	Для каждого НайденныйФайл Из НайденныеФайлы Цикл
		
		УдалитьФайлы(НайденныйФайл.ПолноеИмя);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьЗаписьКласса(Знач ВходящееИмяКласса, Знач ПутьККаталогуКлассов) Экспорт
	
	Возврат Новый ЗаписьBinData(ВходящееИмяКласса, ПутьККаталогуКлассов);

КонецФункции

КлассыПолученияДанных = Новый Соответствие;
КешПутейКлассов = Новый Соответствие;

