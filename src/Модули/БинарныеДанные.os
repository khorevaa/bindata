#Использовать tempfiles

Перем КлассыПолученияДанных;
Перем КешПутейКлассов;

Перем Лог;

Функция ПолучитьФайл(Знач ИмяФайла, Знач КорневаяПапка, Знач ИмяКлассаПолучения) Экспорт

	Если ЕстьКлассПолучения(ИмяКлассаПолучения) Тогда
		Возврат ПолучитьПутьИзКласса(ИмяФайла, ИмяКлассаПолучения);
	КонецЕсли;

	Возврат ОбъединитьПути(КорневаяПапка, ИмяФайла);
	
КонецФункции

Функция ПолучитьПутьИзКласса(Знач ИмяФайла, ИмяКлассаПолучения) Экспорт

	КлючКеша = СтрШаблон("%1_%2", ИмяКлассаПолучения, ИмяФайла);

	ПутьИхКеша = КешПутейКлассов[КлючКеша];

	Если НЕ ПутьИхКеша = Неопределено Тогда
		Возврат ПутьИхКеша;
	КонецЕсли;

	НовыйВременныйФайл = ПолучитьИмяВременногоФайла();
	
	//НовыйВременныйФайл = ВременныеФайлы.НовоеИмяФайла();

	ЗаписатьДанныеВФайл(НовыйВременныйФайл, ПолучитьДанныеФайла(ИмяФайла, ИмяКлассаПолучения));

	КешПутейКлассов.Вставить(КлючКеша, НовыйВременныйФайл);

	Возврат НовыйВременныйФайл;
	
КонецФункции

Функция ПолучитьДанныеФайла(ИмяФайла, ИмяКлассаПолучения)
	
	Возврат КлассыПолученияДанных[ИмяКлассаПолучения].ПолучитьДанные(ИмяФайла);

КонецФункции

Процедура ЗаписатьДанныеВФайл(ИмяВременногоФайла, XMLДанныеФайла)
	
	ДвоичныеДанные = Base64Значение(XMLДанныеФайла);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);

КонецПроцедуры

Функция ЕстьКлассПолучения(ИмяКлассаПолучения)

	Лог.Отладка("Проверяю загрузку класса <%1>", ИмяКлассаПолучения);

	ЗначениеКеша = КлассыПолученияДанных[ИмяКлассаПолучения];
	КлассДоступен = НЕ ЗначениеКеша = Неопределено;
	Если ЗначениеКеша = Неопределено Тогда
		
		КлассДоступен = Ложь;
		Попытка
			Лог.Отладка("Создаю класс <bindata%1>", ИмяКлассаПолучения);
			КлассПолучения = Новый ("bindata"+ИмяКлассаПолучения);
			КлассДоступен = Истина;
		Исключение
			Лог.Отладка("Ошибка загрузки класса <bindata%1>", ИмяКлассаПолучения);
		КонецПопытки;

		Если КлассДоступен Тогда
			КлассыПолученияДанных.Вставить(ИмяКлассаПолучения, КлассПолучения);
		КонецЕсли;

	КонецЕсли;
	Лог.Отладка("Класс <bindata%1> доступен <%2>", ИмяКлассаПолучения, КлассДоступен);
			
	Возврат КлассДоступен;

КонецФункции

Процедура ОчиститьКаталогОтКлассовBindata(Знач РабочийКаталог) Экспорт

	НайденныеФайлы = НайтиФайлы(РабочийКаталог, "bindata*.os", Истина );

	Для каждого НайденныйФайл Из НайденныеФайлы Цикл
		
		УдалитьФайлы(НайденныйФайл.ПолноеИмя);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьЗаписьКласса(Знач ВходящееИмяКласса, Знач ПутьККаталогуКлассов) Экспорт
	
	Возврат Новый ЗаписьBinData(ВходящееИмяКласса, ПутьККаталогуКлассов);

КонецФункции

КлассыПолученияДанных = Новый Соответствие;
КешПутейКлассов = Новый Соответствие;

Лог = Логирование.ПолучитьЛог("oscript.lib.bindata");
//Лог.УстановитьУровень(УровниЛога.Отладка);